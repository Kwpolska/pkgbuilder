# Maintainer: Kwpolska <kwpolska@kwpolska.tk>
pkgname=pkgbuilder-git
pkgver=20120930
pkgrel=1
pkgdesc='A Python AUR helper/library.  Version from git.'
arch=('any')
url='https://github.com/Kwpolska/pkgbuilder'
license=('BSD')
depends=('python' 'pyalpm>=0.5.1-1' 'python-requests')
makedepends=('git')
options=(!emptydirs)
provides=('pkgbuilder')
conflicts=('pkgbuilder')

_gitroot=git://github.com/Kwpolska/pkgbuilder.git
_gitname=pkgbuilder

package() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  python3 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${_gitname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
